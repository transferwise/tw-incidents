import org.eclipse.jgit.api.errors.RefAlreadyExistsException

plugins {
    id "com.github.spotbugs" version "5.0.3" apply false
    id "idea"
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

idea.project {
    vcs = 'Git'
    languageLevel = JavaVersion.VERSION_11
    targetBytecodeVersion = JavaVersion.VERSION_11
}

ext.projectName = 'TransferWise Incidents Registration System'
ext.projectDescription = 'TransferWise Incidents Registration System - uses VictorOps model to register and unregister incidents.'
ext.projectGitHubRepoName = 'tw-incidents'
ext.projectArtifactName = 'tw-incidents'

apply from: 'build.common.gradle'
apply from: 'build.publishing.gradle'

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'org.springframework.boot:spring-boot-actuator'
    compileOnly 'org.springframework:spring-web'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'org.slf4j:slf4j-api'
    implementation 'com.transferwise.common:tw-graceful-shutdown:2.3.0'
    implementation 'com.transferwise.common:tw-graceful-shutdown-interfaces:2.3.0'
    implementation 'com.transferwise.common:tw-base-utils:1.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.apache.commons:commons-lang3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-web'
    testImplementation 'org.springframework.boot:spring-boot-actuator'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
    testImplementation 'org.awaitility:awaitility:4.1.1'
    testImplementation 'javax.servlet:javax.servlet-api'
}

task tagRelease {
    doLast {
        try {
            grgit.tag.add {
                name = "v$version"
                message = "Release of ${version}"
            }
            grgit.push(refsOrSpecs: ["v$version"])
        }
        catch (RefAlreadyExistsException ignored) {
            logger.warn("Tag v$version already exists.")
        }
    }
}

group = "com.transferwise.common"

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

